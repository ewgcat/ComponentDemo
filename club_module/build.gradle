if (isModule.toBoolean()) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'org.greenrobot.greendao'

repositories {
    maven { url "http://dl.bintray.com/fotoapparat/fotoapparat" }
    mavenCentral()
}

android {
    dexOptions {
        javaMaxHeapSize "6g"
    }


    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion

    defaultConfig {
        if (isModule.toBoolean()) {
            applicationId "com.yijian.clubmodule"
            manifestPlaceholders = [
                    JPUSH_PKGNAME: applicationId,
                    JPUSH_APPKEY : "0633251c7577933569229449", //JPush上注册的包名对应的appkey.
                    JPUSH_CHANNEL: "developer-default", //暂时填写默认值即可.
            ]

        }

        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode rootProject.versionCode
        versionName rootProject.versionName
        flavorDimensions '' + rootProject.versionCode
        resConfigs "zh"
        multiDexEnabled true

        //使用renderscript 兼容包
        renderscriptTargetApi 25
        renderscriptSupportModeEnabled true

        vectorDrawables.useSupportLibrary true
        javaCompileOptions.annotationProcessorOptions.includeCompileClasspath = true

        sourceSets {
            main {
                if (isModule.toBoolean()) {
                    manifest.srcFile 'src/main/module/AndroidManifest.xml'
                } else {
                    manifest.srcFile 'src/main/AndroidManifest.xml'
                    //集成开发模式下排除debug文件夹中的所有Java文件
                    java {
                        exclude 'module/clubmodule/login/**'
                    }
                }
            }
        }

        kapt {
            arguments {
                arg("moduleName", project.getName())
            }
        }

    
    }


    productFlavors {
        dev {
            buildConfigField "String", "HOST", "\"$dev_host\""
            buildConfigField "String", "FILE_HOST", "\"$dev_file_host\""
            buildConfigField "String", "H5_HOST", "\"$h5_dev_host\""
        }
        prod {
            buildConfigField "String", "HOST", "\"$prod_host\""
            buildConfigField "String", "FILE_HOST", "\"$prod_file_host\""
            buildConfigField "String", "H5_HOST", "\"$h5_prod_host\""
        }
        train {
            buildConfigField "String", "HOST", "\"$train_host\""
            buildConfigField "String", "FILE_HOST", "\"$train_file_host\""
            buildConfigField "String", "H5_HOST", "\"$train_file_host\""
        }
        sittest {
            buildConfigField "String", "HOST", "\"$test_host\""
            buildConfigField "String", "FILE_HOST", "\"$test_file_host\""
            buildConfigField "String", "H5_HOST", "\"$h5_test_host\""
        }
    }


    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }


}

greendao {
    schemaVersion rootProject.dbVersion
    daoPackage 'com.yijian.clubmodule.greendao.gen'
    targetGenDir 'src/main/java'
}
dependencies {

    api project(':indicatorseekbar')
    api project(':commonlib')
    api 'io.fotoapparat:facedetector:1.0.0'
    api 'io.fotoapparat.fotoapparat:library:1.2.0'

    kapt 'com.github.bumptech.glide:compiler:4.7.1'
    kapt 'com.alibaba:arouter-compiler:1.1.4'
}

apply plugin: 'com.android.application'
apply plugin: 'org.greenrobot.greendao'
apply from: 'tinker-support.gradle'

android {
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion

    defaultConfig {
        applicationId rootProject.applicationId
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode rootProject.versionCode
        versionName rootProject.versionName
        flavorDimensions '' + rootProject.versionCode
        resConfigs "zh"
        multiDexEnabled true
        renderscriptTargetApi 23
        renderscriptSupportModeEnabled true
        javaCompileOptions.annotationProcessorOptions.includeCompileClasspath = true
        ndk {
            abiFilters "armeabi", "x86"
        }

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [moduleName: project.getName()]
            }
        }
        manifestPlaceholders = [
                JPUSH_PKGNAME: applicationId,
                JPUSH_APPKEY : "$jpush_appkey", //JPush上注册的包名对应的appkey.
                JPUSH_CHANNEL: "developer-default", //暂时填写默认值即可.
        ]
    }
    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }
    signingConfigs {
        releaseKey {
            keyAlias release_key_alias
            keyPassword release_key_password
            storeFile new File(rootProject.projectDir, "releaseKey.jks")
            storePassword release_store_password
        }
    }
    buildTypes {
        debug {
            signingConfig signingConfigs.releaseKey
        }
        release {
            //混淆
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.releaseKey
        }
    }
    productFlavors {
        prod {
            buildConfigField "String", "HOST", "\"$prod_host\""
            buildConfigField "String", "FILE_HOST", "\"$prod_file_host\""
        }
        sit {
            buildConfigField "String", "HOST", "\"$sit_host\""
            buildConfigField "String", "FILE_HOST", "\"$sit_file_host\""

        }
    }
    variantFilter { variant ->
        if (variant.buildType.name.equals('release')) {
            variant.flavors.each { flavor ->
                if (!flavor.name.equals("prod")) {
                    variant.setIgnore(true);
                }
            }
        }
    }
    lintOptions {
        abortOnError false
        checkReleaseBuilds false
        disable 'MissingTranslation'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

}

greendao {
    schemaVersion rootProject.dbVersion
    daoPackage 'com.yijian.staff.greendao.gen'
    targetGenDir 'src/main/java'
}
dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    //network
    implementation project(':retrofit2')
    implementation project(':PhotoPicker')
    implementation project(':photoview')
    implementation project(':calendar')

    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
    implementation 'com.squareup.retrofit2:converter-scalars:2.3.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.9.1'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.3'
    implementation 'com.squareup.okhttp3:okhttp:3.9.1'
    implementation 'com.google.code.gson:gson:2.8.1'

    implementation 'com.android.support.constraint:constraint-layout:1.0.2'

    //noinspection GradleCompatible
    implementation "com.android.support:support-core-utils:$supportLibVersion"
    implementation "com.nineoldandroids:library:2.4.0"

    implementation "com.android.support:support-annotations:${rootProject.supportLibVersion}"
    implementation "com.android.support:appcompat-v7:${rootProject.supportLibVersion}"
    implementation "com.android.support:cardview-v7:${rootProject.supportLibVersion}"
    implementation "com.android.support:recyclerview-v7:${rootProject.supportLibVersion}"
    implementation "com.android.support:design:${rootProject.supportLibVersion}"
    implementation "com.android.support:support-v4:${rootProject.supportLibVersion}"
    implementation "com.android.support:palette-v7:${rootProject.supportLibVersion}"

    //ui
    implementation 'com.prolificinteractive:material-calendarview:1.4.3'
    implementation 'com.miguelcatalan:materialsearchview:1.4.0'
    implementation 'net.opacapp:multiline-collapsingtoolbar:1.3.1'
    implementation 'q.rorbin:badgeview:1.1.3'
    implementation 'jp.wasabeef:glide-transformations:3.0.1'
    implementation 'org.sufficientlysecure:html-textview:3.1'
    implementation 'com.google.android:flexbox:0.3.1'
    //rx
    implementation 'com.tbruyelle.rxpermissions2:rxpermissions:0.9.5@aar'
    //图片
    implementation 'com.github.bumptech.glide:glide:4.6.1'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.6.1'
    implementation 'com.github.bumptech.glide:okhttp-integration:4.6.1'
    implementation 'org.jsoup:jsoup:1.10.1'
    //di
    implementation 'com.google.dagger:dagger:2.11'
    annotationProcessor "com.google.dagger:dagger-compiler:2.11"
    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor "com.jakewharton:butterknife-compiler:8.8.1"
    implementation 'org.glassfish:javax.annotation:10.0-b28'
    //other
    implementation 'com.android.support:multidex:1.0.3'
    implementation 'com.tencent.bugly:crashreport_upgrade:latest.release'
    implementation 'com.tencent.bugly:nativecrashreport:latest.release'

    //操作数据库
    implementation 'org.greenrobot:greendao:3.2.2'
    implementation 'org.greenrobot:greendao-generator:3.2.2'
    implementation 'com.github.yuweiguocn:GreenDaoUpgradeHelper:v2.0.2'
    //字体
    implementation 'uk.co.chrisjenx:calligraphy:2.3.0'
    implementation('com.github.niorgai:StatusBarCompat:2.1.4', {
        exclude group: 'com.android.support'
    })
    implementation 'com.jaeger.statusbarutil:library:1.4.0'
    //选择时间，日期，多级联动
    implementation 'com.contrarywind:Android-PickerView:3.2.6'
    implementation 'com.scwang.smartrefresh:SmartRefreshLayout:1.1.0-alpha-1'
    implementation 'com.scwang.smartrefresh:SmartRefreshHeader:1.1.0-alpha-1'
    //没有使用特殊Header，可以不加这行
    implementation 'com.nineoldandroids:library:2.4.0'
    implementation('com.h6ah4i.android.widget.advrecyclerview:advrecyclerview:0.11.0@aar') {
        transitive = true
    }
    implementation 'com.alibaba:arouter-api:1.3.1'
    annotationProcessor 'com.alibaba:arouter-compiler:1.1.4'

    implementation 'cn.jiguang.sdk:jpush:3.1.1'
    implementation 'cn.jiguang.sdk:jcore:1.1.9'
    implementation 'com.github.jwkj:LibZXing:v1.1.2'
}
